BasicGame
    SpriteSet
        background > Immovable img=oryx/floor2_0.png hidden=True
        base > Immovable img=oryx/planet color=WHITE
        avatar > FlakAvatar img=oryx/spaceship1 stype=sam
        missile > Missile
            sam > Missile singleton=True orientation=UP img=oryx/bullet1 color=BLUE
            bomb > Missile orientation=DOWN img=oryx/bullet2 color=RED speed=0.5
        alien > Bomber prob=0.01 stype=bomb cooldown=3 speed=0.8
            alienGreen > Bomber prob=0.01 img=oryx/alien3 stype=bomb cooldown=3 speed=0.8
            alienBlue > Bomber prob=0.01 img=oryx/alien1 stype=bomb cooldown=3 speed=0.8
        portal > hidden=True invisible=True
            portalSlow > SpawnPoint total=20 img=oryx/door2 hidden=True stype=alienBlue cooldown=16 invisible=True
            portalFast > SpawnPoint total=20 img=oryx/dooropen1 hidden=True stype=alienGreen cooldown=12 invisible=True

    LevelMapping
        0 > background base
        1 > background portalSlow
        2 > background portalFast
        A > background avatar
        . > background
        ¡ > background sam
        ¢ > background bomb
        £ > background alienGreen
        ¤ > background alienBlue
        ¥ > background missile
        ¦ > background alien
        § > background portal
        
    InteractionSet
        avatar EOS > stepBack
        alien EOS > turnAround
        missile EOS > killSprite
        base bomb > killBoth
        base sam > killBoth scoreChange=1
        base alien > killSprite
        avatar alien > killSprite scoreChange=-1
        avatar bomb > killSprite scoreChange=-1
        alien sam > killSprite scoreChange=2
        
    TerminationSet
        SpriteCounter stype=avatar limit=0 win=False
        MultiSpriteCounter limit=0 stype1=portal stype2=alien win=True
        
